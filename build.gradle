group 'com.padtools'
version '1.4'

apply plugin: 'java'

sourceCompatibility = 1.8

repositories {
    mavenCentral()
    maven { url 'https://oss.sonatype.org/service/local/repositories/releases/content/' }
}
dependencies {
    implementation 'org.apache.xmlgraphics:batik-svggen:1.14'
    implementation 'org.apache.xmlgraphics:batik-dom:1.14'
    implementation 'com.rover12421.opensource:JFontChooser:1.0.5-3'
    implementation group: 'org.jfree', name: 'jfreesvg', version: '3.3'
    testImplementation group: 'junit', name: 'junit', version: '4.11'
}


def defaultEncoding = 'UTF-8'

compileJava {
    options.encoding = defaultEncoding
}

jar {
    archiveFileName = "PadTools.jar"
    manifest {
        attributes 'Main-Class' : "padtools.Main"
        attributes 'Class-Path' : configurations.runtimeClasspath.collect{'libs/' + it.name}.join(' ')
    }
}

task makeZip(type : Zip, dependsOn: ['jar']) {
    outputs.upToDateWhen { false }

    from 'bundle'
    from 'build/libs/PadTools.jar'

    into ('libs') {
        from configurations.runtimeClasspath
    }
}

compileTestJava {
    options.encoding = defaultEncoding
}
